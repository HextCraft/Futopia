buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

ext.configFile = file('build.properties')

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "${config.major_version}.${config.minor_version}.${config.build_number}"
group= "net.thegaminghuskymc.futopia"
archivesBaseName = config.mod_name

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12-14.21.1.2405"
    runDir = "run"
    mappings = "snapshot_20170716"
    replace '${mod_version}', project.config.mod_version
}

repositories {
	maven { // The repo from which to get waila
        name "Mobius Repo"
        url "http://tehnut.info/maven"
    }
    maven {
        name = "ChickenBones Maven"
        url = "http://chickenbones.net/maven/"
    }
    maven {
        name = "JEI Maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
}

dependencies {
	deobfCompile "mcp.mobius.waila:Hwyla:+:api"
    compile "codechicken:ChickenASM:${config.chicken_version}"
    compile "cofh:RedstoneFlux:${config.mc_version}-${config.rf_version}:deobf"
    compile "cofh:CoFHCore:${config.mc_version}-${config.cofh_core_version}:deobf"
    compile "codechicken:CodeChickenLib:${config.mc_version}-${config.ccl_version}:deobf"
    compile "mezz.jei:jei_${config.mc_version}:${config.jei_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    classifier = 'universal'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives srcJar
}